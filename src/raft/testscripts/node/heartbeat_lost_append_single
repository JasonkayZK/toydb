# A heartbeat response triggers a resend of a lost append.

cluster nodes=3 leader=1
---
n1@1 leader last=1@1 commit=1@1 applied=1 progress={2:1→2 3:1→2}
n2@1 follower(n1) last=1@1 commit=1@1 applied=1
n3@1 follower(n1) last=1@1 commit=1@1 applied=1

# Partition the leader, submit a write whose appends are dropped,
# then heal the partition again.
partition 1
---
n1 ⇹ n2 n3

put 1 foo=bar
---
c1@1 → n1 ClientRequest id=0x01 write 0x0103666f6f03626172
n1@1 append 2@1 put foo=bar
n1@1 ⇥ n2 A̶p̶p̶e̶n̶d̶ ̶b̶a̶s̶e̶=̶1̶@̶1̶ ̶[̶2̶@̶1̶]̶
n1@1 ⇥ n3 A̶p̶p̶e̶n̶d̶ ̶b̶a̶s̶e̶=̶1̶@̶1̶ ̶[̶2̶@̶1̶]̶

heal
---
n1 n2 n3 fully connected

# The next heartbeat will result in match_index=0 since the followers
# don't have the last_index.
heartbeat 1
deliver
---
n1@1 → n2 Heartbeat last_index=2 commit_index=1 read_seq=0
n1@1 → n3 Heartbeat last_index=2 commit_index=1 read_seq=0
n2@1 → n1 HeartbeatResponse match_index=0 read_seq=0
n3@1 → n1 HeartbeatResponse match_index=0 read_seq=0

# The leader has previously matched the followers at index 1.
status 1
---
n1@1 leader last=2@1 commit=1@1 applied=1 progress={2:1→3 3:1→3}

# When it receives the heartbeat response, instead of probing index 1 and then
# sending the actual entries, it simply sends the entries.
deliver
---
n1@1 → n2 Append base=1@1 [2@1]
n1@1 → n3 Append base=1@1 [2@1]

# The followers accept the append and the leader commits and applies.
stabilize
---
n2@1 append 2@1 put foo=bar
n2@1 → n1 AppendResponse match_index=2
n3@1 append 2@1 put foo=bar
n3@1 → n1 AppendResponse match_index=2
n1@1 commit 2@1
n1@1 apply 2@1 put foo=bar
n1@1 → c1 ClientResponse id=0x01 write 0x0102
c1@1 put foo=bar ⇒ 2
